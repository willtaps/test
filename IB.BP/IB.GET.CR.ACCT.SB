*-----------------------------------------------------------------------------
* <Rating>753</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE IB.GET.CR.ACCT.SB( YACC.NO, YR.USER.INFO )

    $INCLUDE I_COMMON
    $INCLUDE I_ENQUIRY.COMMON
    $INCLUDE I_EQUATE
    $INCLUDE I_F.INTERCO.PARAMETER
    $INCLUDE I_F.ACCOUNT
    $INCLUDE I_F.FUNDS.TRANSFER
    $INCLUDE IB.BP I_IB.ATM.MSG.COMMON
    $INCLUDE IB.BP I_F.IB.MESSAGE.TAG.SB
    $INCLUDE IB.BP I_F.IB.TAG.GRP.SB
    $INCLUDE IB.BP I_F.IB.PARAMETER.SB
    $INSERT I_F.IB.MSG.GRP.SB
    $INSERT I_F.IB.ATM.POS.LINK.SB
    $INSERT I_F.IB.BILL.PAYMENTS.ENTITY
    $INSERT I_F.IB.FILE.PARAMETER.SB
    $INSERT IB.BP I_F.IB.ATM.POS.LINK.SB
    $INCLUDE BP I_F.PAYMENT.REFERENCE
    $INCLUDE BP I_F.BSTM.COMPANY

    OWN.BANK.ID = R.IB.PARAMETER.SB< IB.PARM.OWN.BANK.CODE >
    BILL.PAY.SUS =  R.IB.PARAMETER.SB< IB.PARM.BILL.PAY.DEF.ACCT >

    YMSG = "IN.MSG.TYPE =" : IN.MSG.TYPE
    R.IB.ATM.POS.LINK.SB = ""
    NEW.USER.NUMBER = 1

    GOSUB GET.AND.READ.POS.LINK.SB
    BEGIN CASE

    CASE IN.MSG.TYPE[1,1] = "S"
        GOSUB SET.ERMB.SUMM.ACCTS

    CASE IN.MSG.TYPE = "1EORI" OR IN.MSG.TYPE = "2EORI"
        GOSUB SET.EORI.ACCTS

    CASE IN.MSG.TYPE = "2EDST"
        GOSUB GET.CR.OR.DR.ACCT
        GOSUB PROCESS.FILE.ACCOUNTS

    CASE YR.MESSAGE.EVENT = "CASH.WITHDRAWAL" OR YR.MESSAGE.EVENT = "BUY.POS" OR YR.MESSAGE.EVENT = "ADVANCE"
        GOSUB GET.CR.OR.DR.ACCT

    CASE YR.MESSAGE.EVENT = "REFUND"
        GOSUB GET.CR.OR.DR.ACCT

    CASE YR.MESSAGE.EVENT = "POS.CLOSURE"
        GOSUB GET.CR.OR.DR.ACCT
        GOSUB PROCESS.FILE.ACCOUNTS

    CASE YR.MESSAGE.EVENT = "BILL.PAYMENT"
        M.IB.MESSAGE.TAG.SB.ID = IB.BILL.PAY.ID.TAG
        GOSUB GET.TAG.VALUE
        M.IB.BILL.PAYMENTS.ENTITY.ID = TAG.VALUE
        CALL CACHE.READ("F.IB.BILL.PAYMENTS.ENTITY", M.IB.BILL.PAYMENTS.ENTITY.ID, R.IB.BILL.PAYMENTS.ENTITY, YERR)
        IF YERR THEN
            ETEXT = M.IB.BILL.PAYMENTS.ENTITY.ID : " RECORD NOT FOUND IN IB.BILL.PAYMENTS.ENTITY"
            GOSUB LOG.ERROR
            YACC.NO = BILL.PAY.SUS
        END ELSE
            YACC.NO = R.IB.BILL.PAYMENTS.ENTITY< IB.ENTITY.ACCOUNT >
        END

    CASE YR.MESSAGE.EVENT = "INT.BANK.TRF.BEN"
        IF ATM.POS[1,3] = "ATM" THEN
            NIB.ORIGEM = ""
            TAG.NIB.ORIGEM="477"
            LOCATE TAG.NIB.ORIGEM IN MSG.DATA<MSG.TAG.ID,1> SETTING POS THEN
                NIB.ORIGEM = TRIM(MSG.DATA<MSG.TAG.VALUE,POS>)
            END

            IF NIB.ID[1,4] = OWN.BANK.ID AND NIB.ORIGEM[1,4]= OWN.BANK.ID THEN
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT, 1>
            END ELSE
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT, 2>
            END
        END
        ELSE
            GOSUB GET.CR.OR.DR.ACCT
        END
        R.RECORD<FT.DEBIT.ACCT.NO> = YACC.NO
        YACC.NO = ""
        GOSUB GET.NAB.ACCOUNT

    CASE YR.MESSAGE.EVENT = "INT.BANK.TRF.PAY"

        IF ATM.POS[1,3] = "ATM" THEN
            M.IB.MESSAGE.TAG.SB.ID = IB.NIB.ACCT.TAG
            GOSUB GET.TAG.VALUE
            NIB.ID = TAG.VALUE
*Transferencia SB para SB
*2o Passo (Debita Beneficiario e Credita a Conta Suspensa)

            IF NIB.ID[1,4] = OWN.BANK.ID THEN
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT,1>
            END

*Transferencia Emitidas
*SB para OIC (Debita Beneficiario e Credita a Conta Suspensa)

            ELSE
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT,3>
            END
        END
        ELSE
            GOSUB GET.CR.OR.DR.ACCT
        END

    CASE OTHERWISE
        GOSUB GET.CR.OR.DR.ACCT

    END CASE

    IF IN.MSG.TYPE = "3190" THEN
        GOSUB UPDATE.PAYMENT.DETAILS
    END

    RETURN

GET.AND.READ.POS.LINK.SB:

    NIB.ID = ""
    YBANK.ID = ""

    IF YR.MESSAGE.EVENT = "INT.BANK.TRF.BEN" THEN
        M.IB.MESSAGE.TAG.SB.ID = IB.NIB.ACCT.TAG
        GOSUB GET.TAG.VALUE
        NIB.ID = TAG.VALUE
    END ELSE
        M.IB.MESSAGE.TAG.SB.ID = IB.BANK.ID.CODE.TAG
        GOSUB GET.TAG.VALUE
        YBANK.ID = TAG.VALUE
    END

    M.IB.MESSAGE.TAG.SB.ID = IB.TERMINAL.ID.TAG
    GOSUB GET.TAG.VALUE
    YTERM.ID = TAG.VALUE

    M.IB.MESSAGE.TAG.SB.ID = IB.TERM.TYPE.TAG
    GOSUB GET.TAG.VALUE
    YTERM.TYPE = TAG.VALUE

    IF IN.MSG.TYPE = "6EDST" THEN
        YTERM.ID = "0000000000"
    END

    ATM.POS = ""
    BEGIN CASE
    CASE YTERM.TYPE = "A"
        ATM.POS = "ATM"
    CASE YTERM.TYPE = "B"
        ATM.POS = "POS"
    CASE OTHERWISE
        ATM.POS = "POS"
    END CASE

    IF NIB.ID THEN
        IF NIB.ID[1,4] = OWN.BANK.ID THEN
            M.LINK.ID = NIB.ID[1,4] : "." : YTERM.ID : "." : ATM.POS
        END ELSE
            M.LINK.ID ="00": NIB.ID[3,2] : "." : ATM.POS
        END
    END ELSE
        IF YBANK.ID = OWN.BANK.ID THEN
            M.LINK.ID = OWN.BANK.ID : "." :  YTERM.ID : "." : ATM.POS
        END ELSE
            M.LINK.ID = YBANK.ID : "." : ATM.POS
        END
    END

    PART1 = M.LINK.ID[".",1,1]
    IF PART1 = "0000" OR PART1 = OWN.BANK.ID THEN
        ATM.POS = YTERM.TYPE
        CALL IB.GET.TERM.TYPE.SB( ATM.POS)
        IF ATM.POS = "ERROR" THEN
            ATM.POS = "POS"
        END
        M.LINK.ID = PART1 : "." :YTERM.ID : "." : ATM.POS :  "." : IN.MESSAGE.CODE
    END

    GOSUB READ.IB.ATM.POS.LINK.SB

    RETURN

GET.TRANSFER.SUSP:

    YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT, USER.NUMBER>
    IF NOT(YACC.NO) THEN
        YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT,1>
    END

    YMSG = "YACC.NO =" : YACC.NO : " YERR=" : YERR

    GOSUB LOG.ERROR

    RETURN

GET.NAB.ACCOUNT:

    IF NIB.ID[1,4] =  OWN.BANK.ID THEN
        YR.AGENCIA = NIB.ID[6,3]
        IF YR.AGENCIA EQ '000' THEN
* Alteracao para validar NIB REFERENCE - Sheila Osman : 20160711
            GOSUB VALIDATE.REFERENCE
            IF DEVE.USAR.PAYMENT.REFERENCE EQ "1" THEN
                GOSUB BUSCAR.CONTA.COMPANY.PAYMENT.REFERENCE
            END
            YACC.NO = YR.SB.CONTA.COMPANY.PAYMENT.REFERENCE
            GOSUB UPD.PAYM.DETAILS
        END ELSE
* Fim da Alteracao

            CALL CACHE.READ("F.SB.NIB.ACCOUNT", NIB.ID , R.SB.NIB.ACCOUNT, YERR)
            IF R.SB.NIB.ACCOUNT THEN
                YACC.NO = R.SB.NIB.ACCOUNT<1>
            END ELSE
                ETEXT = "NIB RECORD NOT FOUND - " : NIB.ID
                GOSUB LOG.ERROR
            END
        END
    END ELSE

        YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CB.SUS.ACCT, USER.NUMBER>
        IF NOT(YACC.NO) THEN
            YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CB.SUS.ACCT, 1>
        END
        YMSG = "YACC.NO =" : YACC.NO : " YERR=" : YERR
        GOSUB LOG.ERROR
    END


    RETURN

VALIDATE.REFERENCE:
    DEVE.USAR.PAYMENT.REFERENCE = "1"

    GOSUB OPEN.FILES.PAYMENT.REFERENCE
    PAYMENT.REFERENCE.LIST = '' ; PAYMENT.REFERENCE.NO = '' ; YERR = ''
    PAYMENT.REFERENCE.STATUS = ""

*Amended by Mauro Mauane on Feb 2018
    SEL.PAYMENT.REFERENCE = 'SELECT ':FN.EB.PAYMENT.REFERENCE:' WITH NIB EQ ':NIB.ID

    CALL EB.READLIST(SEL.PAYMENT.REFERENCE,PAYMENT.REFERENCE.LIST,'',PAYMENT.REFERENCE.NO,YERR)
    FOR K = 1 TO PAYMENT.REFERENCE.NO
        PAYMENT.REFERENCE.ID = PAYMENT.REFERENCE.LIST<K>
        READ PAYMENT.REFERENCE.RECORD FROM F.EB.PAYMENT.REFERENCE,PAYMENT.REFERENCE.ID ELSE NULL

        PAYMENT.REFERENCE.STATUS = PAYMENT.REFERENCE.RECORD<EB.PRF.STATUS>
        PAYMENT.REFERENCE.COMPANY.CODE = PAYMENT.REFERENCE.RECORD<EB.PRF.COMPANY.CODE>

        YR.NAME = PAYMENT.REFERENCE.RECORD<EB.PRF.NAME>
        YR.MAIN.REF = PAYMENT.REFERENCE.RECORD<EB.PRF.MAIN.REFERENCE>

        IF PAYMENT.REFERENCE.STATUS EQ "ACTIVE" THEN
            PAYMENT.REFERENCE.EXISTE.VALIDA = "1"
        END
        PAYMENT.REFERENCE.EXISTE = "1"
    NEXT K
    RETURN

OPEN.FILES.PAYMENT.REFERENCE:

    FN.EB.PAYMENT.REFERENCE = 'F.EB.PAYMENT.REFERENCE'
    F.EB.PAYMENT.REFERENCE = ''
    CALL OPF(FN.EB.PAYMENT.REFERENCE,F.EB.PAYMENT.REFERENCE)

    FN.EB.BSTM.COMPANY = 'F.EB.BSTM.COMPANY'
    F.EB.BSTM.COMPANY = ''
    CALL OPF(FN.EB.BSTM.COMPANY,F.EB.BSTM.COMPANY)

    RETURN

BUSCAR.CONTA.COMPANY.PAYMENT.REFERENCE:
    YR.SB.CONTA.COMPANY.PAYMENT.REFERENCE = ''

    EB.BSTM.COMPANY.LIST = '' ; EB.BSTM.COMPANY.NO = '' ; YERR = ''
*    Amended by Mauro Mauane on Feb 2018
    CALL F.READ(FN.EB.BSTM.COMPANY,PAYMENT.REFERENCE.COMPANY.CODE,EB.BSTM.COMPANY.LIST,F.EB.BSTM.COMPANY,YERR)

    YR.SB.CONTA.COMPANY.PAYMENT.REFERENCE = EB.BSTM.COMPANY.LIST<EB.BST48.ACCOUNT.NR>
    YR.AGENCIA.BENEF = YR.SB.CONTA.COMPANY.PAYMENT.REFERENCE[1,3]
    YR.CONTA.BENEF = '00':YR.SB.CONTA.COMPANY.PAYMENT.REFERENCE[4,10]

    RETURN

UPD.PAYM.DETAILS:
    R.RECORD<FT.PAYMENT.DETAILS,1> = "PAGAMENTO ESCOLAR"
    R.RECORD<FT.PAYMENT.DETAILS,2> = NIB.ID
    R.RECORD<FT.PAYMENT.DETAILS,3> = YR.MAIN.REF
    R.RECORD<FT.PAYMENT.DETAILS,4> = YR.NAME
    RETURN

GET.CR.OR.DR.ACCT:

    BEGIN CASE

    CASE ATM.POS[1,3] = "ATM"
        IF YR.MESSAGE.EVENT = "INT.BANK.TRF.BEN" THEN
            YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT, USER.NUMBER>
            IF NOT(YACC.NO) THEN
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.TRF.SUS.ACCT, 1>
            END
        END ELSE
            IF YBANK.ID EQ OWN.BANK.ID THEN
                IF YR.MESSAGE.EVENT = "CASH.DEPOSIT" THEN
                    YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.DEP.ACCT,USER.NUMBER>
                    IF NOT(YACC.NO) THEN
                        YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.DEP.ACCT, 1>
                    END
                END ELSE
                    YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.INT.ACCT,USER.NUMBER>
                    IF NOT(YACC.NO) THEN
                        YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.INT.ACCT, 1>
                    END
                END
            END ELSE
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.UOT.SUSP.ACCT,NEW.USER.NUMBER>
                IF NOT(YACC.NO) THEN
                    YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.UOT.SUSP.ACCT,1>
                END
            END
        END

    CASE OTHERWISE
        IF YBANK.ID EQ OWN.BANK.ID THEN
            YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.MRCH.SUSP.ACCT, USER.NUMBER>
            IF NOT(YACC.NO) THEN
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.MRCH.SUSP.ACCT,1>
            END
        END ELSE
            YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.UOT.SUSP.ACCT, NEW.USER.NUMBER>
            IF NOT(YACC.NO) THEN
                YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.UOT.SUSP.ACCT, 1>
            END
        END
    END CASE

    YMSG = "YACC.NO =" : YACC.NO : " YERR=" : YERR
    GOSUB LOG.ERROR

    RETURN

GET.TAG.VALUE:

    TAG.VALUE = ""
    LOCATE M.IB.MESSAGE.TAG.SB.ID IN MSG.DATA<MSG.TAG.ID,1> SETTING POS THEN
        TAG.VALUE = TRIM(MSG.DATA<MSG.TAG.VALUE,POS>)
    END

    RETURN

PROCESS.FILE.ACCOUNTS:

    BEGIN CASE

    CASE IN.MSG.TYPE = "2EDST"
        GOSUB GET.CR.OR.DR.ACCT
        R.RECORD<FT.DEBIT.ACCT.NO> = R.IB.FILE.PARAMETER.SB<IB.FILE.PARM.EDST.VISA.SUSP.AC>
        R.RECORD<FT.CHARGE.CODE> = "WAIVE"
        LOCATE "329" IN MSG.DATA<MSG.TAG.ID,1> SETTING POS THEN
            YR.OUT.ACCT = TRIM(MSG.DATA<MSG.TAG.VALUE,POS>)
            YR.IN.ACCT=""
            YR.INTERFACE.SAVE = YR.INTERFACE
            YR.INTERFACE = ""
            CALL IB.FMT.ACCOUNT.SB (YR.IN.ACCT , YR.OUT.ACCT)
            YR.INTERFACE = YR.INTERFACE.SAVE
            R.RECORD<FT.CREDIT.ACCT.NO> = YR.IN.ACCT
            YACC.NO = YR.IN.ACCT
        END


    CASE IN.MSG.TYPE = "3EDST"
        GOSUB GET.CR.OR.DR.ACCT
        IF NOT(POS.PL) THEN
            DR.ACCT = R.RECORD<FT.DEBIT.ACCT.NO>
            R.RECORD<FT.DEBIT.ACCT.NO> = YACC.NO
            YACC.NO = DR.ACCT
        END ELSE
            YR.PETROL.MERCHANT = R.IB.ATM.POS.LINK.SB<IB.LINK.PROCESS.COMM>[1,1]
            YR.DR.SUSP.ACCT = R.IB.ATM.POS.LINK.SB<IB.LINK.MRCH.SUSP.ACCT>
            LOCATE IB.TERMINAL.ID.TAG IN YR.MESSAGE.ARRAY<MSG.TAG.ID,1> SETTING POS ELSE
                YR.MESSAGE.ARRAY<MSG.TAG.ID,POS> = IB.TERMINAL.ID.TAG
            END
            YR.MESSAGE.ARRAY<2,POS> = "0000000000"
            GOSUB GET.CR.OR.DR.ACCT
            YACC.NO = "PL":R.IB.ATM.POS.LINK.SB<IB.LINK.EDST.PL.CATEG>
            IF YR.PETROL.MERCHANT = "N" THEN
                R.RECORD<FT.DEBIT.ACCT.NO> = YR.DR.SUSP.ACCT
            END
        END
        GOSUB UPDATE.PAYMENT.DETAILS

    CASE IN.MSG.TYPE = "6EDST"
        LOCATE "318" IN MSG.DATA<MSG.TAG.ID,1> SETTING POS THEN
            YSIGN = TRIM(MSG.DATA<MSG.TAG.VALUE,POS>)
        END
        YACCT = R.RECORD<FT.DEBIT.ACCT.NO>
        SUS.ACCT = R.IB.ATM.POS.LINK.SB<IB.LINK.EDST.SUS.ACCT>
        IF YSIGN = "C" THEN
            R.RECORD<FT.DEBIT.ACCT.NO> = SUS.ACCT
            R.RECORD<FT.CREDIT.ACCT.NO> = YACCT
            YACC.NO = YACCT
        END ELSE
            R.RECORD<FT.DEBIT.ACCT.NO> = YACCT
            R.RECORD<FT.CREDIT.ACCT.NO> = SUS.ACCT
            YACC.NO = SUS.ACCT
        END
        GOSUB UPDATE.PAYMENT.DETAILS
    END CASE

    RETURN

UPDATE.PAYMENT.DETAILS:

    YL.NAME = R.IB.ATM.POS.LINK.SB<IB.LINK.NAME>
    R.RECORD<FT.PAYMENT.DETAILS> = YL.NAME
    PRINT R.RECORD<FT.PAYMENT.DETAILS>
    R.FUNDS.TRASFER<FT.PAYMENT.DETAILS> = R.IB.ATM.POS.LINK.SB<IB.LINK.NAME>
    RETURN


SET.EORI.ACCTS:

    GOSUB SET.ERMB.SUMM.ACCTS

    RETURN

SET.ERMB.SUMM.ACCTS:

    GOSUB GET.CR.OR.DR.ACCT

    IF EORI.TAG.330 THEN
        R.RECORD<FT.DEBIT.ACCT.NO> = R.IB.FILE.PARAMETER.SB<IB.FILE.PARM.EORI.CASH.SUSP.AC>
        R.RECORD<FT.CREDIT.ACCT.NO> = "MZN10" : YTERM.ID[8]
    END ELSE
        R.RECORD<FT.DEBIT.ACCT.NO> = R.IB.ATM.POS.LINK.SB<IB.LINK.TOU.SUSP.ACCT>
        R.RECORD<FT.CREDIT.ACCT.NO> = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.INT.ACCT>
    END

    YACC.NO = R.IB.ATM.POS.LINK.SB<IB.LINK.CASH.INT.ACCT>

    YR.ID.COMPANY = "MZ001" : RIGHT(YTERM.ID ,4)
    LOCATE YR.ID.COMPANY IN R.INTERCO.PARAMETER<ST.ICP.COMPANY,1> SETTING COM.POS ELSE
        YR.ID.COMPANY =  "MZ001" : YTERM.ID[5,4]
        LOCATE YR.ID.COMPANY IN R.INTERCO.PARAMETER<ST.ICP.COMPANY,1> SETTING COM.POS ELSE
            YR.ID.COMPANY = ""
        END
    END
    IF  YR.ID.COMPANY THEN
        CALL LOAD.COMPANY(YR.ID.COMPANY)
        FN.ACCOUNT = "F.ACCOUNT" : FM : "NO.FATAL.ERROR"
        F.ACCOUNT = ""
        CALL OPF(FN.ACCOUNT, F.ACCOUNT)
    END ELSE
        ETEXT = "INVALID COMPANY.ID DERIVED FROM TERMINAL.ID [" : YTERM.ID : "]"
        GOSUB LOG.ERROR
    END

    RETURN

*----------------------------
READ.IB.ATM.POS.LINK.SB:
*----------------------------

    PART1 = M.LINK.ID[".",1,1]
    PART2 = M.LINK.ID[".",2,1]
    PART3 = M.LINK.ID[".",3,1]
    PART4 = M.LINK.ID[".",4,1]

    IDS.CHECKED = M.LINK.ID
    M.IB.ATM.POS.LINK.SB.ID = M.LINK.ID

    YMSG = " M.IB.ATM.POS.LINK.SB.ID=" :  M.IB.ATM.POS.LINK.SB.ID
    GOSUB LOG.ERROR

    R.IB.ATM.POS.LINK.SB = ""
    GOSUB READ.IB.ATM.POS.LINK.SB.FILE

    IF R.IB.ATM.POS.LINK.SB = "" THEN
        M.IB.ATM.POS.LINK.SB.ID = PART1:".": PART2 : "." : PART3
        YMSG = " M.IB.ATM.POS.LINK.SB.ID=" :  M.IB.ATM.POS.LINK.SB.ID
        GOSUB LOG.ERROR
        IDS.CHECKED := " - " : M.IB.ATM.POS.LINK.SB.ID
        GOSUB READ.IB.ATM.POS.LINK.SB.FILE
    END

    IF R.IB.ATM.POS.LINK.SB = "" THEN
        M.IB.ATM.POS.LINK.SB.ID = PART1:".": PART2
        YMSG = " M.IB.ATM.POS.LINK.SB.ID=" :  M.IB.ATM.POS.LINK.SB.ID
        GOSUB LOG.ERROR
        IDS.CHECKED := " - " : M.IB.ATM.POS.LINK.SB.ID
        GOSUB READ.IB.ATM.POS.LINK.SB.FILE
    END

    IF R.IB.ATM.POS.LINK.SB = "" THEN
        M.IB.ATM.POS.LINK.SB.ID = PART1:".": ATM.POS
        YMSG = " M.IB.ATM.POS.LINK.SB.ID=" :  M.IB.ATM.POS.LINK.SB.ID
        GOSUB LOG.ERROR
        IDS.CHECKED := " - " : M.IB.ATM.POS.LINK.SB.ID
        GOSUB READ.IB.ATM.POS.LINK.SB.FILE
    END

    IF YERR AND (PART1 = "0000" OR PART1 EQ OWN.BANK.ID ) THEN
        IF PART2 <> "POS" OR PART2 <> "ATM" THEN
            IF PART1 EQ OWN.BANK.ID THEN
                M.LINK.ID = "0000." : ATM.POS
            END ELSE
                M.LINK.ID = PART1:".POS"
            END
            M.IB.ATM.POS.LINK.SB.ID = M.LINK.ID
            YMSG = " M.IB.ATM.POS.LINK.SB.ID=" :  M.IB.ATM.POS.LINK.SB.ID
            GOSUB LOG.ERROR
            IDS.CHECKED := " - " : M.IB.ATM.POS.LINK.SB.ID
            GOSUB READ.IB.ATM.POS.LINK.SB.FILE
        END
    END

    IF R.IB.ATM.POS.LINK.SB = "" THEN
        ETEXT = "ERR0R: RECORD " : IDS.CHECKED : " : NOT FOUND ON FILE IB.ATM.POS.LINK.SB"
        GOSUB LOG.ERROR
    END

    RETURN

*---------
LOG.ERROR:
*---------

    IF ETEXT THEN
        IB.LOG.LEVEL = "ERROR"
    END ELSE
        ETEXT = YMSG
    END

    CALL IB.ATM.LOG.ERROR( SYSTEM(40))

    ETEXT = ""

    IB.LOG.LEVEL = "DEBUG"

    RETURN

*----------------------------
READ.IB.ATM.POS.LINK.SB.FILE:
*----------------------------

    YERR = ""
    R.IB.ATM.POS.LINK.SB = ""
    CALL F.READ(FN.IB.ATM.POS.LINK.SB, M.IB.ATM.POS.LINK.SB.ID, R.IB.ATM.POS.LINK.SB, F.IB.ATM.POS.LINK.SB, YERR)

    RETURN

END
