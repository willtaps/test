    SUBROUTINE IB.GET.COMM.CHG.SB(YR.COMM.CODE, YR.CHARGE.CODE)

    $INCLUDE I_COMMON
    $INCLUDE I_EQUATE
    $INCLUDE I_F.INTERCO.PARAMETER
    $INCLUDE I_F.ACCOUNT
    $INCLUDE I_F.CARD.ISSUE
    $INCLUDE I_F.COMPANY
    $INCLUDE IB.BP I_IB.ATM.MSG.COMMON
    $INCLUDE IB.BP I_IB.TAG.SB.COMMON
    $INCLUDE IB.BP I_F.SB.IB.CHARGES

    GOSUB INITIALISE

    GOSUB PROCESS

    RETURN

PROCESS:

    M.EB.SB.IB.CHARGES.ID = IN.MESSAGE.CODE

    M.IB.MESSAGE.TAG.SB.ID = "3"
    GOSUB GET.TAG.VALUE
    M.EB.SB.IB.CHARGES.ID := "." : TAG.VALUE

    M.IB.MESSAGE.TAG.SB.ID = "261"
    GOSUB GET.TAG.VALUE
    YR.BIN = TAG.VALUE

    M.IB.MESSAGE.TAG.SB.ID = "319"
    GOSUB GET.TAG.VALUE
    YR.EXBIN = TAG.VALUE

    SEL.CMD = "SELECT " : FN.SB.PARAMETRO.IB : " WITH BIN EQ [" : YR.BIN : "] AND EXBIN EQ [" : YR.EXBIN : "] SAVING UNIQUE TIPPROD"
    YR.CARD.TYPE = ""
    CALL EB.READLIST(SEL.CMD, YR.CARD.TYPE, "","","")

    YMSG = "BIN VALUE = " : YR.BIN : " EXBIN " : YR.EXBIN : " CODE=" : YR.CARD.TYPE
    GOSUB LOG.ERROR

    M.EB.SB.IB.CHARGES.ID := "." : YR.CARD.TYPE

    M.IB.MESSAGE.TAG.SB.ID = "226"
    GOSUB GET.TAG.VALUE
    YR.COUNTRY = TAG.VALUE

    M.IB.MESSAGE.TAG.SB.ID = "241"
    GOSUB GET.TAG.VALUE
    YR.BANK = TAG.VALUE


    YR.CODTRN = IN.MESSAGE.CODE
    IF YR.CODTRN NE "37" THEN
        IF YR.COUNTRY EQ "508" THEN
            YR.INOUT = "IN"
        END ELSE
            YR.INOUT = "OUT"
        END
    END ELSE
        IF YR.BANK EQ "0003" THEN
            YR.INOUT = "IN"
        END ELSE
            YR.INOUT = "OUT"
        END
    END

    M.EB.SB.IB.CHARGES.ID := "." : YR.INOUT
    YMSG = "M.EB.SB.IB.CHARGES.ID=" : M.EB.SB.IB.CHARGES.ID
    GOSUB LOG.ERROR

    CALL F.READ(FN.EB.SB.IB.CHARGES, M.EB.SB.IB.CHARGES.ID, R.EB.SB.IB.CHARGES, F.EB.SB.IB.CHARGES, YERR)

    YR.CHARGE.CODE = R.EB.SB.IB.CHARGES<EB.SB.38.FT.CHARGE.TYPE>
    YR.COMM.CODE = R.EB.SB.IB.CHARGES<EB.SB.38.FT.COMM.TYPE>

    RETURN

INITIALISE:

    FN.EB.SB.IB.CHARGES = "F.EB.SB.IB.CHARGES"
    F.EB.SB.IB.CHARGES = ""
    CALL OPF(FN.EB.SB.IB.CHARGES, F.EB.SB.IB.CHARGES)

    FN.SB.PARAMETRO.IB = "F.SB.PARAMETRO.IB"
    F.SB.PARAMETRO.IB = ""
    CALL OPF(FN.SB.PARAMETRO.IB, F.SB.PARAMETRO.IB)

    RETURN

GET.TAG.VALUE:

    TAG.VALUE = ""
    LOCATE M.IB.MESSAGE.TAG.SB.ID IN MSG.DATA<MSG.TAG.ID,1> SETTING POS THEN
        TAG.VALUE = TRIM(MSG.DATA<MSG.TAG.VALUE,POS>)
    END

    RETURN

LOG.ERROR:

    IF ETEXT THEN
        IB.LOG.LEVEL = "ERROR"
    END ELSE
        ETEXT = YMSG
    END

    G$THIS = SYSTEM(40)

    CALL IB.ATM.LOG.ERROR(G$THIS )

    ETEXT = ""

    IB.LOG.LEVEL = "DEBUG"

    RETURN

END
