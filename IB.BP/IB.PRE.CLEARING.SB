*-----------------------------------------------------------------------------
* <Rating>40</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE IB.PRE.CLEARING.SB ( YR.IN.FILE )

    $INSERT ../T24_BP I_COMMON
    $INSERT ../T24_BP I_EQUATE
    $INSERT ../T24_BP I_GTS.COMMON
    $INSERT ../T24_BP I_TSA.COMMON
    $INSERT ../T24_BP I_F.TSA.STATUS
    $INSERT ../T24_BP I_BATCH.FILES
    $INSERT ../T24_BP I_F.TSA.SERVICE
    $INSERT ../T24_BP I_F.USER
    $INCLUDE IB.BP I_IB.ATM.MSG.COMMON
    $INCLUDE IB.BP I_IB.ATM.CLEARING.COMMON
    $INSERT IB.BP I_F.IB.CLEARING.CONTROL.SB
    $INSERT IB.BP I_F.IB.EDST.SUMM.SB
    $INSERT IB.BP I_F.IB.FILE.PARAMETER.SB
    $INSERT IB.BP I_F.IB.PARAMETER.SB
    $INSERT I_F.IB.FILE.TAGS.SB
    $INSERT I_F.IB.MESSAGE.TAG.SB
    $INSERT I_F.IB.TAG.GRP.SB

    GOSUB PROCESS

    RETURN

*-------
PROCESS:
*-------

    CNT = 0
    OPENSEQ IN.WORK.PATH, YR.IN.FILE READONLY TO F.INPUT THEN
        LOOP
            READSEQ YLINE FROM F.INPUT ELSE EXIT
            YR.FILE.TYPE = YR.IN.FILE["_",1,1]
            YRECORD.TYPE = YLINE[1,1]
            YR.FILE.JULDATE = YR.IN.FILE["_",3,1][1,8]
            YR.FILE.SUFFIX = UPCASE(YR.IN.FILE[2])
            YR.MESSAGE.ARRAY = ""
            BEGIN CASE
            CASE YRECORD.TYPE = "0"
                M.IB.FILE.TAGS.SB.ID = "HEADER"
                GOSUB READ.IB.FILE.TAGS.SB.FILE
                GOSUB MAP.RECORD
            CASE YRECORD.TYPE = 9
                M.IB.FILE.TAGS.SB.ID = "FOOTER"
                GOSUB READ.IB.FILE.TAGS.SB.FILE
                GOSUB MAP.RECORD
            CASE OTHERWISE
                M.IB.FILE.TAGS.SB.ID = YR.FILE.TYPE : "-" : YRECORD.TYPE
                GOSUB READ.IB.FILE.TAGS.SB.FILE
                GOSUB MAP.RECORD
            END CASE
            CNT += 1
            YR.REC.NUMBER = FMT(CNT,'R%10')
            M.IB.CLEARING.CONTROL.SB.ID = YR.FILE.TYPE :YRECORD.TYPE:YR.REC.NUMBER:YR.FILE.SUFFIX
            READ R.IB.CLEARING.CONTROL.SB FROM F.IB.CLEARING.CONTROL.SB, M.IB.CLEARING.CONTROL.SB.ID THEN
                CALL OCOMO("DUPLICATE ID --> " : M.IB.CLEARING.CONTROL.SB.ID )
            END ELSE
                R.IB.CLEARING.CONTROL.SB<IB.CROL.MESSAGE> = YLINE
                R.IB.CLEARING.CONTROL.SB<IB.CROL.FILE.NAME> = YR.IN.FILE
                R.IB.CLEARING.CONTROL.SB<IB.CROL.JUL.DATE> = YR.FILE.JULDATE
                R.IB.CLEARING.CONTROL.SB<IB.CROL.STATUS> = 0
                R.IB.CLEARING.CONTROL.SB<IB.CROL.TAG>=YR.MESSAGE.ARRAY<MSG.TAG.ID>
                R.IB.CLEARING.CONTROL.SB<IB.CROL.DATA>=YR.MESSAGE.ARRAY<MSG.TAG.VALUE>
                WRITE R.IB.CLEARING.CONTROL.SB TO F.IB.CLEARING.CONTROL.SB, M.IB.CLEARING.CONTROL.SB.ID
            END
        REPEAT
        CLOSESEQ F.INPUT
    END

    RETURN

*----------
MAP.RECORD:
*----------

    YR.TERM = ""
    YR.CODE.TXN = ""

    YR.POS =1
    YR.MESSAGE.ARRAY = ""
    FILE.POS = 1
    NO.TAGS = DCOUNT(R.IB.FILE.TAGS.SB<IB.FL.TAGS.TAG.NO>, VM )
    FOR NN = 1 TO  NO.TAGS
        M.IB.MESSAGE.TAG.SB.ID = R.IB.FILE.TAGS.SB<IB.FL.TAGS.TAG.NO,NN>
        GOSUB READ.IB.MESSAGE.TAG.SB.FILE
        YTAG.LENTH = R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.LENGHT>
        YVALUE = YLINE[FILE.POS,YTAG.LENTH]
        FILE.POS += YTAG.LENTH

        IF M.IB.MESSAGE.TAG.SB.ID  =  YR.MESSAGE.CODE THEN
            YR.VARIABLE.TAG = YVALUE
        END
        LOCATE M.IB.MESSAGE.TAG.SB.ID IN FILE.VARIABLE.DATA SETTING V.POS THEN
            VARIABLE.DATA = YVALUE
            GOSUB PROCESS.VARIABLE.DATA
        END ELSE
            YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID
            YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
            YR.POS += 1
        END

    NEXT NN

    RETURN

*---------------------
PROCESS.VARIABLE.DATA:
*---------------------

    VAR.POS = 1

    VAR.DATA.ID = M.IB.FILE.TAGS.SB.ID : "." : YR.TERM : YR.CODE.TXN
    R.VAR.TAGS = ""   ; YERR = ""
    CALL F.READ(FN.IB.FILE.TAGS.SB, VAR.DATA.ID, R.VAR.TAGS, F.IB.FILE.TAGS.SB, YERR)

    IF YERR THEN
        YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID
        YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
        YR.POS += 1
    END ELSE
        NO.VAR.TAGS = DCOUNT(R.VAR.TAGS<IB.FL.TAGS.TAG.NO>, VM )
        FOR VV = 1 TO  NO.VAR.TAGS
            M.IB.MESSAGE.TAG.SB.ID = R.VAR.TAGS<IB.FL.TAGS.TAG.NO,VV>
            GOSUB READ.IB.MESSAGE.TAG.SB.FILE
            YTAG.LENTH = R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.LENGHT>
            YVALUE = VARIABLE.DATA[VAR.POS,YTAG.LENTH]
            VAR.POS += YTAG.LENTH
            YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID
            YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
            YR.POS += 1
        NEXT VV
    END

    RETURN

*---------------------------
READ.IB.MESSAGE.TAG.SB.FILE:
*---------------------------
    CALL F.READ(FN.IB.MESSAGE.TAG.SB, M.IB.MESSAGE.TAG.SB.ID, R.IB.MESSAGE.TAG.SB, F.IB.MESSAGE.TAG.SB, YERR)
    RETURN
*-----------------------
READ.IB.TAG.GRP.SB.FILE:
*-----------------------
    CALL F.READ(FN.IB.TAG.GRP.SB, M.IB.TAG.GRP.SB.ID, R.IB.TAG.GRP.SB, F.IB.TAG.GRP.SB, YERR)
    RETURN

*-------------------------
READ.IB.FILE.TAGS.SB.FILE:
*-------------------------
    CALL F.READ(FN.IB.FILE.TAGS.SB, M.IB.FILE.TAGS.SB.ID, R.IB.FILE.TAGS.SB, F.IB.FILE.TAGS.SB, "")
    RETURN

END
