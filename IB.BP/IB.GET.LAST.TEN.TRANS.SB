    SUBROUTINE IB.GET.LAST.TEN.TRANS.SB ( YACCT.ID, YTRANS.ARRAY, YNO)
*-----------------------------------------------------------------------------
* <Rating>940</Rating>
*-----------------------------------------------------------------------------
    $INCLUDE I_COMMON
    $INCLUDE I_EQUATE
    $INCLUDE I_F.STMT.ENTRY
    $INCLUDE I_F.ACCOUNT
    $INCLUDE I_F.ACCOUNT.STATEMENT
    $INCLUDE I_F.TRANSACTION
    $INCLUDE I_F.INTERCO.PARAMETER
    $INCLUDE IB.BP I_F.IB.ATM.BALANCE.SB

    GOSUB INITIALISE
    GOSUB PROCESS

    RETURN

INITIALISE:

    Y.CONTINUE = 0

    BR.CODE.ID = YACCT.ID[1,3]
    LOCATE BR.CODE.ID IN R.INTERCO.PARAMETER<ST.ICP.BRANCH.CODE,1> SETTING BR.CODE THEN
        MNE=  R.INTERCO.PARAMETER<ST.ICP.FIN.MNEMONIC,BR.CODE>
        Y.CONTINUE = 1

        FN.ACCOUNT = "F" : MNE : ".ACCOUNT" : FM : "NO.FATAL.ERROR"
        F.ACCOUNT = ""
        CALL OPF(FN.ACCOUNT, F.ACCOUNT)

        FN.ACCT.STMT.PRINT= "F" : MNE : ".ACCT.STMT.PRINT" : FM : "NO.FATAL.ERROR"
        F.ACCT.STMT.PRINT = ""
        CALL OPF(FN.ACCT.STMT.PRINT, F.ACCT.STMT.PRINT)

        FN.STMT.ENTRY = "F" : MNE : ".STMT.ENTRY" : FM : "NO.FATAL.ERROR"
        F.STMT.ENTRY = ""
        CALL OPF(FN.STMT.ENTRY, F.STMT.ENTRY)

        FN.STMT.PRINTED = "F" : MNE : ".STMT.PRINTED" : FM : "NO.FATAL.ERROR"
        F.STMT.PRINTED = ""
        CALL OPF(FN.STMT.PRINTED, F.STMT.PRINTED)

        FN.TRANSACTION = "F.TRANSACTION" : FM : "NO.FATAL.ERROR"
        F.TRANSACTION = ""
        CALL OPF(FN.TRANSACTION, F.TRANSACTION)
    END

    RETURN

PROCESS:

    IF NOT(Y.CONTINUE) THEN
        GOSUB FILL.WITH.ZEROS
        RETURN
    END

    YCNT = 0

    M.ACCOUNT.ID = YACCT.ID
    READ R.ACCOUNT FROM F.ACCOUNT, M.ACCOUNT.ID THEN

        IF R.ACCOUNT<AC.CURRENCY> = LCCY THEN
            AMT.POS = AC.STE.AMOUNT.LCY
        END ELSE
            AMT.POS = AC.STE.AMOUNT.FCY
        END

        CALL F.READ(FN.ACCT.STMT.PRINT, M.ACCOUNT.ID, R.ACCT.STMT.PRINT, F.ACCT.STMT.PRINT, "")

        NO.STA = DCOUNT(R.ACCT.STMT.PRINT, @FM)

        FOR J = NO.STA TO 1 STEP -1
            M.STA.ID = R.ACCT.STMT.PRINT<J>
            STMT.DATE = M.STA.ID["/",1,1]
            M.STMT.PRINTED.ID = M.ACCOUNT.ID : "-" :M.STA.ID["/",1,1]
            CALL F.READ(FN.STMT.PRINTED, M.STMT.PRINTED.ID, R.STMT.PRINTED, F.STMT.PRINTED, "")
            NO.STMT.ENT = DCOUNT(R.STMT.PRINTED, @FM)
            FOR K = NO.STMT.ENT TO 1 STEP -1
                M.STMT.ENTRY.ID = R.STMT.PRINTED<K>
                CALL F.READ(FN.STMT.ENTRY, M.STMT.ENTRY.ID, R.STMT.ENTRY, F.STMT.ENTRY, "")
                YCNT += 1
                IF YCNT > YNO THEN EXIT
                YTRANS.ARRAY<IB.BAL.STMT.DATE,YCNT> = R.STMT.ENTRY<AC.STE.BOOKING.DATE>
                YAMT = R.STMT.ENTRY<AMT.POS>
                YTRANS.ARRAY<IB.BAL.STMT.SIGN,YCNT> = IFS(YAMT < 0 , "D", "C")
                YTRANS.ARRAY<IB.BAL.STMT.AMOUNT,YCNT> = INT(ABS(YAMT * 100))
                YTXN.CODE =  R.STMT.ENTRY<AC.STE.TRANSACTION.CODE>
                CALL CACHE.READ(FN.TRANSACTION, YTXN.CODE, R.TRANSACTION, YERR)
                YTRANS.ARRAY<IB.BAL.STMT.DESC,YCNT> =  R.TRANSACTION< AC.TRA.SHORT.DESC >
                YTRANS.ARRAY<IB.BAL.STMT.TXN.CODE,YCNT> = YTXN.CODE
                IF YCNT >= YNO THEN EXIT
            NEXT K
            IF YCNT >= YNO THEN EXIT
        NEXT J

    END

    YTRANS.ARRAY<IB.BAL.STMT.NO.TRANS> = YCNT

    RETURN

FILL.WITH.ZEROS:

    YCNT = 1
    YAMT = 0
    YTRANS.ARRAY<IB.BAL.STMT.DATE,YCNT> = 0
    YTRANS.ARRAY<IB.BAL.STMT.SIGN,YCNT> = IFS(YAMT < 0 , "D", "C")
    YTRANS.ARRAY<IB.BAL.STMT.AMOUNT,YCNT> = INT(ABS(YAMT * 100))
    YTXN.CODE =  0
    YTRANS.ARRAY<IB.BAL.STMT.DESC,YCNT> =  0
    YTRANS.ARRAY<IB.BAL.STMT.TXN.CODE,YCNT> = 0
    YTRANS.ARRAY<IB.BAL.STMT.NO.TRANS> = 0

    RETURN

END
