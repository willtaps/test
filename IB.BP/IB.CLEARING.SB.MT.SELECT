    SUBROUTINE IB.CLEARING.SB.MT.SELECT

    $INCLUDE ../T24_BP I_COMMON
    $INCLUDE ../T24_BP I_EQUATE
    $INCLUDE ../T24_BP I_GTS.COMMON
    $INCLUDE ../T24_BP I_TSA.COMMON
    $INCLUDE ../T24_BP I_F.TSA.STATUS
    $INCLUDE ../T24_BP I_BATCH.FILES
    $INCLUDE ../T24_BP I_F.TSA.SERVICE
    $INCLUDE ../T24_BP I_F.USER
    $INCLUDE IB.BP I_IB.ATM.MSG.COMMON
    $INCLUDE IB.BP I_IB.TAG.SB.COMMON
    $INCLUDE IB.BP I_F.IB.CORR.FILE.SB
    $INCLUDE IB.BP I_F.IB.ERMB.SB
    $INSERT I_F.IB.FILE.TAGS.SB
    $INSERT I_F.IB.MESSAGE.TAG.SB
    $INSERT I_F.IB.TAG.GRP.SB
    $INSERT I_F.IB.PARAMETER.SB
    $INSERT I_F.IB.EDST.SUMM.SB
!    $INCLUDE SB.SPARROW.BP I_F.SB.SPARROW.ATM.TXN.NO
    $INSERT IB.BP I_F.IB.FILE.PARAMETER.SB
    $INSERT IB.BP I_F.IB.ATM.POS.LINK.SB
    $INSERT IB.BP I_F.IB.ATM.MESSAGE.LOG.SB
    $INSERT IB.BP I_IB.CLEARING.SB.MT.COMMON
    $INSERT IB.BP I_F.IB.CLEARING.SB.MT.LOG
*
    GOSUB INITIALISE
    GOSUB OPEN.TEMP.FILE
    GOSUB CHK.PROCESS.CNTRL.LIST
    RETURN
************************
CHK.PROCESS.CNTRL.LIST:
*------------------------
    BEGIN CASE
    CASE CONTROL.LIST<1,1> = 'PROCESS'
        GOSUB PROCESS
    CASE CONTROL.LIST<1,1>[1,4] = 'EDST'
        READ Y.EDST.LINE FROM F.TEMP.PATH,'EDST' THEN
            CALL F.DELETE(FN.TEMP.PATH,'EDST')
            CALL BATCH.BUILD.LIST('',Y.EDST.LINE)
        END
    CASE CONTROL.LIST<1,1>[1,4] = 'ERMB'
        READ Y.ERMB.LINE FROM F.TEMP.PATH,'ERMB' THEN
            CALL F.DELETE(FN.TEMP.PATH,'ERMB')
            CALL BATCH.BUILD.LIST('',Y.ERMB.LINE)
        END
    CASE CONTROL.LIST<1,1>[1,4] = 'EORI'
        READ Y.EORI.LINE FROM F.TEMP.PATH,'EORI' THEN
            CALL F.DELETE(FN.TEMP.PATH,'EORI')
            CALL BATCH.BUILD.LIST('',Y.EORI.LINE)
        END
    CASE CONTROL.LIST<1,1>[1,4] = 'CCLN'
        READ Y.CCLN.LINE FROM F.TEMP.PATH,'CCLN' THEN
            CALL F.DELETE(FN.TEMP.PATH,'CCLN')
            CALL BATCH.BUILD.LIST('',Y.CCLN.LINE)
        END
*    CASE CONTROL.LIST<1,1>[1,4] = 'OTHR'
*        CONTROL.LIST<1,1>:='*':Y.OTHR.FILE
*        CALL BATCH.BUILD.LIST('',Y.OTHR.LINE)
    CASE CONTROL.LIST<1,1> = 'ACLK'
        GOSUB CHK.ACLK.PROCESS.BUILD
    END CASE
    RETURN
*---------------------
CHK.ACLK.PROCESS.BUILD:
*---------------------
!     CALL SB.UNLOCK.EVENT.IB
    Y.ALL.LOCKED.LIST=''
    CO.LIST = '' ; CO.NO = '' ; CO.ERR = ''
    SEL.CO = 'SSELECT ':FN.COMPANY:' WITH CONSOLIDATION.MARK EQ "N" SAVING  MNEMONIC'
    CALL EB.READLIST(SEL.CO,CO.LIST,'',CO.NO,CO.ERR)
    LOOP
        REMOVE YMNE FROM CO.LIST SETTING MORE1
    WHILE YMNE:MORE1
        GOSUB OPEN.ALE.FILE
        GOSUB GET.LOCKED.EVENT
    REPEAT
    WRITE Y.ALL.LOCKED.LIST TO F.TEMP,"ALE_P24"
    CALL BATCH.BUILD.LIST('',Y.ALL.LOCKED.LIST)
    YR.INTERFACE = ""
    RETURN
*------------------------
OPEN.TEMP.FILE:
*------------------------
    FN.TEMP.PATH="TEMP"
    OPEN "",FN.TEMP.PATH TO F.TEMP.PATH ELSE
        CALL OCOMO("CANNOT OPEN FILE")
    END
    RETURN
*--------------
INITIALISE:
*--------------
    IF NOT(CONTROL.LIST) THEN
        CONTROL.LIST<-1> = 'PROCESS'
        CONTROL.LIST<-1> = 'EDST'
        CONTROL.LIST<-1> = 'ERMB'
        CONTROL.LIST<-1> = 'EORI'
        CONTROL.LIST<-1> = 'CCLN'
        CONTROL.LIST<-1> = 'ACLK'
        Y.EDST.LINE = ''   ;  Y.ERMB.LINE = ''
        Y.EORI.LINE = ''   ;  Y.CCLN.LINE = ''
        Y.OTHR.LINE = ''
    END
    RETURN
*-------
PROCESS:
*-------

    CALL OCOMO("ENTER MAIN LOOP....")
    YFILE.LIST = ""
    YFILE.LIST.CCLN=''
    YFILE.LIST.EDST=''
    YFILE.LIST.EORI=''
    YFILE.LIST.ERMB=''
    YFILE.NO = 0
    CMD = "SELECT " : FN.IN.FILE : " WITH @ID UNLIKE PROCESSED..."
    EXEC CMD
    LOOP
        READNEXT YR.IN.FILE ELSE
            YR.IN.FILE = ""
        END
    WHILE YR.IN.FILE DO
        BEGIN CASE
        CASE INDEX(YR.IN.FILE,'CCLN',1)
            YFILE.LIST.CCLN<-1> = YR.IN.FILE
        CASE INDEX(YR.IN.FILE,'EDST',1)
            YFILE.LIST.EDST<-1> = YR.IN.FILE
        CASE INDEX(YR.IN.FILE,'EORI',1)
            YFILE.LIST.EORI<-1> = YR.IN.FILE
        CASE INDEX(YR.IN.FILE,'ERMB',1)
            YFILE.LIST.ERMB<-1> = YR.IN.FILE
        CASE 1
            YFILE.LIST<-1> = YR.IN.FILE
        END CASE
        YFILE.NO += 1
    REPEAT
    YFILE.LIST<-1> = YFILE.LIST.EDST:@FM:YFILE.LIST.ERMB:@FM:YFILE.LIST.EORI:@FM:YFILE.LIST.CCLN

 !    FOR FF = 1 TO YFILE.NO
 !        YR.IN.FILE = YFILE.LIST<FF>
 !        CALL OCOMO("PROCESSING FILE " : YR.IN.FILE)
 !    GOSUB PROCESS.FILE
 !    NEXT FF
 *****
     LOOP
         YR.IN.FILE=''
         REMOVE YR.IN.FILE FROM YFILE.LIST SETTING YPOS
     WHILE YR.IN.FILE:YPOS
         IF YR.IN.FILE THEN
             CALL OCOMO("PROCESSING FILE " : YR.IN.FILE)
             GOSUB PROCESS.FILE
         END ELSE
             CALL OCOMO("EMPTY FILE PROCESSING SKIPPED " : YR.IN.FILE)
         END
     REPEAT
 *****
    RETURN
*-------------------------
READ.IB.FILE.TAGS.SB.FILE:
*-------------------------
    CALL F.READ(FN.IB.FILE.TAGS.SB, M.IB.FILE.TAGS.SB.ID, R.IB.FILE.TAGS.SB, F.IB.FILE.TAGS.SB, ERR.IB.FILE.TAGS.SB)
    RETURN
**************
MAP.RECORD:
*-------------
    YR.TERM = ""  ;     YR.CODE.TXN = ""
    CALL OCOMO("MAPPING RECORD")
    YR.POS =1     ;     YR.MESSAGE.ARRAY = ""
    FILE.POS = 1
    NO.TAGS = DCOUNT(R.IB.FILE.TAGS.SB<IB.FL.TAGS.TAG.NO>, VM )
    FOR NN = 1 TO  NO.TAGS
        GOSUB MAP.SUB.PROCESS
    NEXT NN
    GOSUB LOG.MESSAGE
    RETURN
*----------------
MAP.SUB.PROCESS:
*----------------
    M.IB.MESSAGE.TAG.SB.ID = R.IB.FILE.TAGS.SB<IB.FL.TAGS.TAG.NO,NN>
    GOSUB READ.IB.MESSAGE.TAG.SB.FILE
    YTAG.LENTH = R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.LENGHT>
    YVALUE = YLINE[FILE.POS,YTAG.LENTH]
    FILE.POS += YTAG.LENTH
    IF M.IB.MESSAGE.TAG.SB.ID  =  YR.MESSAGE.CODE THEN
        YR.VARIABLE.TAG = YVALUE
    END
    LOCATE M.IB.MESSAGE.TAG.SB.ID IN FILE.VARIABLE.DATA SETTING V.POS THEN
        VARIABLE.DATA = YVALUE
        GOSUB PROCESS.VARIABLE.DATA
    END ELSE
        YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID * 1
        YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
        YR.POS += 1
        CALL OCOMO( "TAG=:" : M.IB.MESSAGE.TAG.SB.ID : " YVALUE=:" : YVALUE )
    END
    BEGIN CASE
    CASE M.IB.MESSAGE.TAG.SB.ID = "003"
        YR.TERM = YVALUE
    CASE  M.IB.MESSAGE.TAG.SB.ID = "120"
        YR.CODE.TXN = YVALUE
    END CASE

    IF M.IB.FILE.TAGS.SB.ID = "HEADER" AND R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.CONSTANT.VALUE,1>[1,1] = "!" THEN
        BEGIN CASE
        CASE R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.CONSTANT.VALUE,1>[2,99] = "YR.FILE.TYPE"
            YR.FILE.TYPE = YVALUE
        CASE OTHERWISE
            NULL
        END CASE
    END
    RETURN
*------------
PROCESS.FILE:
*------------
    YR.PROCESS.EORI = @FALSE
    YR.FILE.TYPE = ""
    YR.FILE.JULDATE = ""
    Y.FINAL.LINE = ''
    Y.PR.CTRL = ''
    LINE.CNT = ''
    OPENSEQ OUT.PATH, YR.IN.FILE READONLY TO F.INPUT THEN
        GOSUB OPEN.OUTPUT.FILE
        LOOP
            READSEQ YLINE FROM F.INPUT ELSE
                EXIT
            END
            CALL OCOMO('Line From File: ':YLINE)
!Ãº - xFA ASCII 250 - converted to u
            CONVERT CHARX(249) TO ' ' IN YLINE
            CONVERT CHARX(250) TO 'u' IN YLINE
            CONVERT CHARX(251) TO ' ' IN YLINE
            CONVERT CHARX(255) TO ' ' IN YLINE
            CALL OCOMO('Line After Cov: ':YLINE)

            LINE.CNT+=1
            YRECORD.TYPE = YLINE[1,1]
            Y.WR.FILE = YR.IN.FILE:'.':SESSION.NO:'*':FMT(LINE.CNT,"R%7")
            GOSUB CHK.FILE.PROCESS
        REPEAT

        BEGIN CASE
        CASE Y.PR.CTRL = 'EDST'
            Y.EDST.LINE<-1> = Y.FINAL.LINE
            GOSUB WRITE.TEMP.FILE.LINES
        CASE Y.PR.CTRL = 'ERMB'
            Y.ERMB.LINE<-1> = Y.FINAL.LINE
            GOSUB WRITE.TEMP.FILE.LINES
        CASE Y.PR.CTRL = 'EORI'
            GOSUB WRITE.TEMP.FILE.LINES
            Y.EORI.LINE<-1> = Y.FINAL.LINE
        CASE Y.PR.CTRL = 'CCLN'
            Y.CCLN.LINE<-1> = Y.FINAL.LINE
            GOSUB WRITE.TEMP.FILE.LINES
        CASE 1
            M.IB.FILE.TAGS.SB.ID = YR.FILE.TYPE : "-" : YRECORD.TYPE
            GOSUB READ.IB.FILE.TAGS.SB.FILE
            GOSUB MAP.RECORD
            YMSG.LOG = "RESPONSE : NULL - MESSAGE NOT MAPPED"
            GOSUB WRITE.FILE.LOG
        END CASE
    END
    CLOSESEQ F.INPUT
    GOSUB IB.ARC.PROCESS.FILE
    RETURN
*--------------------
CHK.FILE.PROCESS:
*--------------------
    BEGIN CASE
    CASE YLINE[1,1] EQ '0'
        Y.PR.CTRL = YLINE[3,4]
        M.IB.FILE.TAGS.SB.ID = "HEADER"
        GOSUB READ.IB.FILE.TAGS.SB.FILE
        GOSUB MAP.RECORD
        YR.FILE.TYPE = YLINE[3,4]
        CALL OCOMO("HEADER:" : YR.FILE.TYPE)
        YMSG.LOG = "REQUEST :":YLINE
        YMSG.LOG<1,-1> = "RESPONSE : HEADER"
        GOSUB WRITE.FILE.LOG
        GOSUB HDR.PROCESS
    CASE YLINE[1,1] EQ '9'
        M.IB.FILE.TAGS.SB.ID = "FOOTER"
        CALL OCOMO("FOOTER:" : YR.FILE.TYPE)
        GOSUB READ.IB.FILE.TAGS.SB.FILE
        GOSUB MAP.RECORD
        YMSG.LOG = "REQUEST :":YLINE
        YMSG.LOG<1,-1> =  "RESPONSE : FOOTER"
        GOSUB WRITE.FILE.LOG
    CASE 1
        GOSUB FORM.ARR.LINE
    END CASE
    RETURN
******************
OPEN.ALE.FILE:
******************
    FN.AC.LOCKED.EVENTS = 'F':YMNE:'.AC.LOCKED.EVENTS'
    FV.AC.LOCKED.EVENTS = ""
    CALL OPF(FN.AC.LOCKED.EVENTS,FV.AC.LOCKED.EVENTS)

    YCO.ID = ''
    YMNE.CO = ''
    ER2 = ''
    CALL F.READ(FN.MNEMONIC.COMPANY,YMNE,YMNE.CO,F.MNEMONIC.COMPANY,ER2)
    YCO.ID = YMNE.CO<1>
    RETURN
*************
GET.LOCKED.EVENT:
*************
    LOCKED.LIST = '' ; LOCKED.NO = '' ; LOCKED.ERRO = ''
    SEL.LOCKED = 'SELECT ':FN.AC.LOCKED.EVENTS:' WITH TO.DATE EQ ':TODAY:' AND DESCRIPTION LIKE ...INTERBANCO...'
    CALL EB.READLIST(SEL.LOCKED,LOCKED.LIST,'',LOCKED.NO,LOCKED.ERRO)

    FOR YR.LOCKED = 1 TO LOCKED.NO
        LOCKED.ID = LOCKED.LIST<YR.LOCKED>
        Y.ALL.LOCKED.LIST <-1> = 'AC.LOCKED.EVENTS,REVE/R/PROCESS,OFSUSER/123456/':YCO.ID:',':LOCKED.ID:',0'
    NEXT YR.LOCKED
    RETURN
*-----------------------
WRITE.TEMP.FILE.LINES:
*-----------------------
    Y.PREV.LINE = ''
    READ Y.PREV.LINE FROM F.TEMP.PATH,Y.PR.CTRL THEN
    END
    IF Y.PREV.LINE THEN
        Y.FL.LINE = Y.PREV.LINE:FM:Y.FINAL.LINE
    END ELSE
        Y.FL.LINE = Y.FINAL.LINE
    END
    WRITE Y.FL.LINE TO F.TEMP.PATH,Y.PR.CTRL
    RETURN
*----------------
OPEN.OUTPUT.FILE:
*----------------
*Changes done for the Mutithreaded
*    YR.ARC.FILE = YR.IN.FILE:".":SESSION.NO:".":TIME()
*        YR.ARC.FILE = YR.IN.FILE[1,4]:'.':SESSION.NO:".":TIME()
    YR.ARC.FILE = YR.IN.FILE:'.':SESSION.NO
    OPENSEQ ARC.PATH , "PROCESSED." : YR.ARC.FILE TO F.OUTPUT ELSE
        CREATE F.OUTPUT ELSE
            NULL
        END
    END
    RETURN
****************
IB.ARC.PROCESS.FILE:
**********************
    COPY.AND.DEL = @TRUE
    IF YR.FILE.TYPE = "EORI" AND NOT(YR.PROCESS.EORI) THEN
        COPY.AND.DEL = @FALSE
    END
    IF COPY.AND.DEL THEN
        CMD = "COPY FROM " : FN.IN.FILE : " TO " : FN.IN.ARC : " " : YR.IN.FILE : "," : YR.ARC.FILE
        CALL OCOMO(CMD)
        EXEC CMD
        DELETE F.IN.FILE, YR.IN.FILE
        CALL OCOMO("DELETING FILE " : FN.IN.FILE : " " : YR.IN.FILE)
***IF YR.FILE.TYPE = "EDST" THEN
        IF Y.PR.CTRL = 'EDST' THEN
*Processamento do sweeping da Coca-Cola
            CALL V.SB.SWEEP.PAG.SERV
            R.IB.EORI.JULDATE.SB = TIMEDATE()
            M.IB.EORI.JULDATE.SB.ID = YR.FILE.TYPE : "." : YR.FILE.JULDATE
            CALL OCOMO("WRITE-" : FN.IB.EORI.JULDATE.SB : ">" : M.IB.EORI.JULDATE.SB.ID)
            WRITE R.IB.EORI.JULDATE.SB TO F.IB.EORI.JULDATE.SB,M.IB.EORI.JULDATE.SB.ID
        END
        CLOSESEQ F.OUTPUT
    END
    RETURN
*----------------------
PROCESS.VARIABLE.DATA:
*----------------------
    VAR.POS = 1
    CALL OCOMO("VAR.DATA=" : VARIABLE.DATA )
    VAR.DATA.ID = M.IB.FILE.TAGS.SB.ID : "." : YR.TERM : YR.CODE.TXN
    CALL OCOMO("VAR.DATA.ID=": VAR.DATA.ID)
    R.VAR.TAGS = ""   ; YERR = ""
    CALL F.READ(FN.IB.FILE.TAGS.SB, VAR.DATA.ID, R.VAR.TAGS, F.IB.FILE.TAGS.SB, YERR)
    IF YERR THEN
        CALL OCOMO("VAR.DATA NOT FOUND RETURN FULL STRING ....")
        YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID * 1
        YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
        YR.POS += 1
        CALL OCOMO( "TAG=:" : M.IB.MESSAGE.TAG.SB.ID : " YVALUE=:" : YVALUE )
    END ELSE
        NO.VAR.TAGS = DCOUNT(R.VAR.TAGS<IB.FL.TAGS.TAG.NO>, VM )
        FOR VV = 1 TO  NO.VAR.TAGS
            M.IB.MESSAGE.TAG.SB.ID = R.VAR.TAGS<IB.FL.TAGS.TAG.NO,VV>
            GOSUB READ.IB.MESSAGE.TAG.SB.FILE
            YTAG.LENTH = R.IB.MESSAGE.TAG.SB<IB.MSG.TAG.LENGHT>
            YVALUE = VARIABLE.DATA[VAR.POS,YTAG.LENTH]
            VAR.POS += YTAG.LENTH
            YR.MESSAGE.ARRAY<1, YR.POS> =  M.IB.MESSAGE.TAG.SB.ID * 1
            YR.MESSAGE.ARRAY<2,YR.POS> = YVALUE
            YR.POS += 1
            CALL OCOMO( "TAG=:" : M.IB.MESSAGE.TAG.SB.ID : " YVALUE=:" : YVALUE )
        NEXT VV
    END
    RETURN
*---------------
WRITE.FILE.LOG:
*---------------
    IF YR.EDST.SUMM THEN
        RETURN
    END
    CALL OCOMO("WRITE-" : YMSG.LOG)
*    WRITESEQ YMSG.LOG APPEND TO F.OUTPUT ELSE NULL
    CALL F.WRITE(FN.IB.CLEARING.SB.MT.LOG,Y.WR.FILE,YMSG.LOG)
    RETURN
*----------------------------
READ.IB.MESSAGE.TAG.SB.FILE:
*----------------------------
    CALL F.READ(FN.IB.MESSAGE.TAG.SB, M.IB.MESSAGE.TAG.SB.ID, R.IB.MESSAGE.TAG.SB, F.IB.MESSAGE.TAG.SB, YERR)
    RETURN
*------------
LOG.MESSAGE:
*------------
    CALL OCOMO("LOG MESSAGE")
    IF YR.FILE.TYPE THEN
        IF YRECORD.TYPE <> "0" AND  YRECORD.TYPE <> "9" THEN
            YR.FILE.NAME = YR.FILE.TYPE
*CALL IB.MSG.LOG.RUN.SB(YR.FILE.NAME)
        END
    END
    RETURN
*-----------
LOG.ERROR:
*-----------
    CALL OCOMO(YMSG)
    RETURN
*******************
HDR.PROCESS:
*******************
    LOCATE "110" IN  YR.MESSAGE.ARRAY<1,1> SETTING POS THEN
        YR.FILE.JULDATE = YR.MESSAGE.ARRAY<2,POS>
        IF YR.FILE.TYPE = "EORI" THEN
            M.IB.EORI.JULDATE.SB.ID = "EDST." : YR.FILE.JULDATE
            YMSG = "CHECKING FOR JULDATE FILE " : FN.IB.EORI.JULDATE.SB : ">":  M.IB.EORI.JULDATE.SB.ID
            GOSUB LOG.ERROR
            READ R.IB.EORI.JULDATE.SB FROM F.IB.EORI.JULDATE.SB, M.IB.EORI.JULDATE.SB.ID THEN
                YR.PROCESS.EORI = @TRUE
            END
            CALL OCOMO("FILE FOUND ? " : IFS(YR.PROCESS.EORI=@TRUE,"YES","NO"))
        END
    END
    RETURN
******************
FORM.ARR.LINE:
******************
    IF Y.PR.CTRL EQ 'EORI' THEN
        Y.FINAL.LINE<-1> = YLINE:'|':YR.IN.FILE:'.':SESSION.NO:'|':YR.PROCESS.EORI:'|':FMT(LINE.CNT,"R%7"):'|':YR.FILE.JULDATE
    END ELSE
        Y.FINAL.LINE<-1> = YLINE:'|':YR.IN.FILE:'.':SESSION.NO:'|':FMT(LINE.CNT,"R%7"):'|':YR.FILE.JULDATE
    END
    RETURN
END
